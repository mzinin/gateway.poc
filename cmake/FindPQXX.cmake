IF (PQXX_USE_STATIC_LIBS)
  SET (CMAKE_FIND_LIBRARY_SUFFIXES .a)
ENDIF ()

FIND_PATH (PQ_INCLUDE_DIR NAMES libpq-fe.h PATH_SUFFIXES include include/postgresql)
FIND_LIBRARY (PQ_LIBRARY NAMES pq)

FIND_PATH (PQXX_INCLUDE_DIR NAMES pqxx/pqxx PATH_SUFFIXES include)
FIND_LIBRARY (PQXX_LIBRARY NAMES pqxx)

FIND_LIBRARY (PGCOMMON_LIBRARY NAMES pgcommon PATH_SUFFIXES postgresql postgresql/14/lib)
FIND_LIBRARY (PGPORT_LIBRARY NAMES pgport PATH_SUFFIXES postgresql postgresql/14/lib)

IF (PQXX_INCLUDE_DIR AND EXISTS "${PQXX_INCLUDE_DIR}/pqxx/version.hxx")
    FILE (STRINGS "${PQXX_INCLUDE_DIR}/pqxx/version.hxx" PQXX_VERSION_CONTENT REGEX "^#define PQXX_VERSION \".+\"$")
    STRING (REGEX REPLACE "^#define PQXX_VERSION \"([^\"]+)\"$" "\\1" PQXX_VERSION_STRING "${PQXX_VERSION_CONTENT}")
    UNSET (PQXX_VERSION_CONTENT)
ENDIF ()

SET (PQXX_LIBRARIES
    ${PQXX_LIBRARY}
    ${PQ_LIBRARY}
    ${PGCOMMON_LIBRARY}
    ${PGPORT_LIBRARY}
)

INCLUDE (FindPackageHandleStandardArgs)

FIND_PACKAGE_HANDLE_STANDARD_ARGS (PQXX
    REQUIRED_VARS PQXX_LIBRARY PQ_LIBRARY PGCOMMON_LIBRARY PGPORT_LIBRARY PQXX_INCLUDE_DIR PQ_INCLUDE_DIR
    VERSION_VAR PQXX_VERSION_STRING
)
